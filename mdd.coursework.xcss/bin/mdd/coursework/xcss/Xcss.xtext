grammar mdd.coursework.xcss.Xcss with org.eclipse.xtext.common.Terminals

generate xcss "http://www.coursework.mdd/xcss/Xcss"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

XcssModel:
	elements+=AbstractElement*
;

AbstractElement:
	VariableDeclaration | XCSSStyleRule
;

XCSSStyleRule:
	selectors+=XCSSSelector ( ',' selectors+=XCSSSelector )*
	BEGIN
		variableDeclarations+=VariableDeclaration*
		cssStyleDeclarations+=CSSStyleDelacration*
	END
;

// variable declaration

VariableDeclaration:
	name=VariableId '=' value=Addition
;

VariableId:
	'$' ID
;

// calculation

Addition returns Expression:
	Multiplication ({Addition.left = current} operator+=('+'|'-') right+=Multiplication)*
;

Multiplication returns Expression:
	Primary ({Multiplication.left = current} operator+=('*'|'/') right+=Primary)*
;

Primary returns Expression:
	CSSStyleValue | '(' Addition ')'
;

// CSS selector

XCSSSelector:
	name=SelectorName (pseudoClasses+=PseudoClass)*
;

SelectorName:
	dot?=('.')? name=ID | asterisk = '*'
;

PseudoClass:
	':' value=ID
;

// CSS style declaration

CSSStyleDelacration:
	cssStyleKey=CSSStyleKey ':' cssStyleValue=CSSStyleValue
;

CSSStyleKey:
	value=ID
;

CSSStyleValue:
	value=CSSStyleValueLiteral | variable=[VariableDeclaration|VariableId]
;

CSSStyleValueLiteral:
	number=NumberLiteral | size=SizeLiteral | word=ID | hex=HEX | function=FUNCTION_EXPRESSION | string=STRING
;

SizeLiteral:
	value=NUMBER unit=ID
;

NumberLiteral:
	value=NUMBER
;

NUMBER:
	INT | FLOAT
;

FLOAT returns ecore::EFloat hidden():
	('-')? INT* '.' INT+
;

// Terminals

@Override
terminal ID:
    '^'?('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'-')*;
    
terminal HEX:
	'#' ('a'..'f'|'A'..'F'|'0'..'9')*
;

terminal FUNCTION_EXPRESSION:
	(('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'-')*'(')->')'
;

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END'; 
